apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

// For dependencies license report
// To use: ./gradlew license<BuildVariant>Report
apply plugin: 'com.cmgapps.licenses'
licenses {
    reports {
        // Enable this JSON generation to get the full data structure
        // json {
        //    enabled = true
        //    destination = file("$buildDir/reports/licenses/dependencies-licenses.json")
        //}

        html.enabled = false // html is enabled by default

        // Generate CSV format
        custom {
            enabled = true
            destination = file("$buildDir/reports/licenses/dependencies-licenses.csv")
            generate {
                final Collection list -> list.collect{
                    [
                        (it.mavenCoordinates.groupId + '.' + it.mavenCoordinates.artifactId) + (it.name != null ? ' - ' + it.name : ''),
                        it.description != null ? it.description.replaceAll('[;\\n\\r]+', ' ') : '',
                        it.licenses[0] != null ? it.licenses[0].name : '',
                        it.licenses[0] != null ? it.licenses[0].url : '',
                    ].join(';')
                }.join('\n') }
        }
    }
}

android {
    compileSdk 34
    namespace 'com.poupa.vinylmusicplayer'
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 33
        renderscriptTargetApi 33

        vectorDrawables.useSupportLibrary = true

        applicationId 'com.poupa.vinylmusicplayer'
        versionCode 201
        versionName '1.10.4'

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'
        }
    }
    signingConfigs {
        ci {
            final String keystorePropertiesFile = rootProject.file('keystore.properties')
            final Properties keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    flavorDimensions = ['default']
    productFlavors {
        playStore {
            dimension 'default'
            signingConfig signingConfigs.ci
        }
        fdroid {
            dimension 'default'
        }
        ci {
            dimension 'default'

            final String gitBranch = providers.exec {commandLine('git', 'rev-parse', '--abbrev-ref', 'HEAD')}.standardOutput.asText.get().trim()
            final String gitSha1 = providers.exec {commandLine('git', 'rev-parse', '--short', 'HEAD')}.standardOutput.asText.get().trim()
            final String buildTime = new Date().format('yyMMdd-HHmm')
            final String buildId = gitBranch + '_' + gitSha1 + '_' + buildTime

            applicationIdSuffix '.ci'
            versionNameSuffix '-' + buildId + ' CI'
            signingConfig signingConfigs.ci
            archivesBaseName = 'vinyl_' + buildId
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
    kotlin {
        jvmToolchain(17)
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
        buildConfig true
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lint {
        abortOnError false
        disable 'MissingTranslation', 'InvalidPackage'
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.fragment:fragment-ktx:1.7.1'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.media:media:1.7.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    // TODO Upgrade to the Kotlin version - the Java one is not maintained
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.afollestad:material-cab:2.0.1'

    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.11.0'
    // TODO Right now cannot migrate KAPT to KSP, since we still use KAPT dataBinding
    //      Not clear if KSP supports it - https://issuetracker.google.com/issues/173030256#comment10
    //noinspection KaptUsageInsteadOfKsp
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    // TODO Replace this library (not updated since years, no doc) by a modern alternative
    implementation 'com.github.kabouzeid:app-theme-helper:1.3.10'
    implementation 'com.github.kabouzeid:RecyclerView-FastScroll:1.0.16-kmod'
    implementation 'com.github.kabouzeid:SeekArc:1.2-kmod'
    implementation 'com.github.kabouzeid:AndroidSlidingUpPanel:6'

    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'

    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'

    implementation('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar') {
        transitive = true
    }

    implementation 'com.heinrichreimersoftware:material-intro:2.0.0'

    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'

    implementation 'io.noties.markwon:core:4.6.2'
    implementation 'io.noties.markwon:html:4.6.2'
    implementation 'io.noties.markwon:image-glide:4.6.2'
    implementation 'io.noties.markwon:linkify:4.6.2'

    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'

    implementation 'com.github.VinylMusicPlayer:jaudiotagger:v3.0.1o'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testImplementation 'junit:junit:4.13.2'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
}
