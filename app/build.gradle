apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 33
    namespace 'com.poupa.vinylmusicplayer'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        renderscriptTargetApi 29

        vectorDrawables.useSupportLibrary = true

        applicationId 'com.poupa.vinylmusicplayer'
        versionCode 178
        versionName '1.4.0'

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'
        }
    }
    signingConfigs {
        ci {
            storeFile file("../script/ci-unsecured.jks")
            // TODO Update password to drop 'phono'
            keyAlias = "phono-unsecured"
            storePassword = "phono-unsecured"
            keyPassword = "phono-unsecured"
        }
    }
    flavorDimensions "default"
    productFlavors {
        playStore {
            dimension "default"
        }
        fdroid {
            dimension "default"
        }
        ci {
            dimension "default"

            String gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
            String gitSha1 = 'git rev-parse --short HEAD'.execute().text.trim()
            String buildTime = new Date().format('yyMMdd-HHmm')
            String buildId = gitBranch + "_" + gitSha1 + "_" + buildTime

            applicationIdSuffix ".ci"
            versionNameSuffix "-" + buildId + " CI"
            signingConfig signingConfigs.ci
            archivesBaseName = 'vinyl2_' + buildId
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget=11
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lint {
        abortOnError false
        disable 'MissingTranslation', 'InvalidPackage'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.7.21"

    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    implementation 'com.google.android.material:material:1.7.0'

    implementation 'com.github.kabouzeid:app-theme-helper:1.3.10'
    implementation 'com.github.kabouzeid:RecyclerView-FastScroll:1.0.16-kmod'
    implementation 'com.github.kabouzeid:SeekArc:1.2-kmod'
    implementation 'com.github.kabouzeid:AndroidSlidingUpPanel:6'

    // TODO Upgrade to the Kotlin version - the Java one is not maintained
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.afollestad:material-cab:0.1.12'

    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'
    implementation('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar') {
        transitive = true
    }

    //implementation 'org.bitbucket.ijabz:jaudiotagger:3.0.1'
    implementation 'net.jthink:jaudiotagger:3.0.1'

    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.google.code.gson:gson:2.8.9'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    testImplementation 'junit:junit:4.12'

    implementation 'com.heinrichreimersoftware:material-intro:2.0.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
}
