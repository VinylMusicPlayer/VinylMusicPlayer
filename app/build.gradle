buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        //noinspection GradleDynamicVersion
    }
}

apply plugin: 'com.android.application'

static def execAndCaptureOutput(String cmd) {
    def output = ''
    try {
        def proc = cmd.execute()
        proc.in.eachLine { line -> output = line }
        proc.err.eachLine { line -> println line }
        proc.waitFor()
        if (proc.exitValue() != 0) {
          output = ''
        }
    } catch (Exception e) {
        // Do nothing
        println e
    }
    return output
}

static def gitBranch() {
    return execAndCaptureOutput('git rev-parse --abbrev-ref HEAD')
}

static def gitSha1() {
    return execAndCaptureOutput('git rev-parse --short HEAD')
}

static def buildTime() {
  return new Date().format('yyMMdd-HHmm')
}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'
    dataBinding {
        enabled = true
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27

        renderscriptTargetApi 27
        vectorDrawables.useSupportLibrary = true

        applicationId 'com.poupa.vinylmusicplayer'
        versionCode 163
        versionName '0.20.2'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG '
        }
    }
    signingConfigs {
        circleci {
            storeFile file("../.circleci/circleci-unsecured.jks")
            keyAlias = "phono-unsecured"
            storePassword = "phono-unsecured"
            keyPassword = "phono-unsecured"
        }
    }
    flavorDimensions "audience"
    productFlavors {
        production {
            dimension "audience"
        }
        circleci {
            dimension "audience"

            signingConfig signingConfigs.circleci
            def buildId = "_" + buildTime() + "_" + gitSha1()
            versionNameSuffix buildId
         }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "vinyl-${variant.name}-${variant.versionName}.apk"
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    supportLibVersion = '27.1.1'
}

dependencies {
    implementation "com.android.support:support-compat:$supportLibVersion"
    implementation "com.android.support:support-core-utils:$supportLibVersion"
    implementation "com.android.support:support-core-ui:$supportLibVersion"
    implementation "com.android.support:support-media-compat:$supportLibVersion"
    implementation "com.android.support:support-fragment:$supportLibVersion"
    implementation "com.android.support:support-v13:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:gridlayout-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "com.android.support:percent:$supportLibVersion"
    implementation "com.android.support:preference-v7:$supportLibVersion"
    implementation "com.android.support:preference-v14:$supportLibVersion"
    implementation 'com.github.kabouzeid:app-theme-helper:1.3.10'
    implementation 'com.github.kabouzeid:RecyclerView-FastScroll:1.0.16-kmod'
    implementation 'com.github.kabouzeid:SeekArc:1.2-kmod'
    implementation 'com.github.kabouzeid:AndroidSlidingUpPanel:3.3.0-kmod3'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.afollestad:material-cab:0.1.12'
    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0'
    implementation 'com.heinrichreimersoftware:material-intro:1.6'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
    implementation 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.github.AdrienPoupa:jaudiotagger:2.2.3'
    implementation('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar') {
        transitive = true
    }
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
}
